#!/bin/tcsh

unalias *

where mc > /dev/null    && alias mc mc -b
where grep > /dev/null  && alias i grep -i
where egrep > /dev/null && alias i egrep -i
egrep --color a ~/.aliases >& /dev/null && alias i egrep -i --color
alias e       i -v
ls-F -l >& /dev/null && alias ls ls-F
alias ll      ls -lA
alias l       ls -lA
alias lstd    "ls -lFA --time-style=+%D | grep `date +%D`"
alias mv      mv -i
alias ms      man -k
alias p       ping
alias rd	    screen -xRR
where tmux > /dev/null && alias rd tmux attach
alias realias	source ~/.aliases
alias ressh   source ~/.ssh_agent
alias s       ssh
alias skm     ssh -O exit
alias sfm     ssh -O forward 
alias t       telnet
alias h       history 25
alias j       jobs -l
where colordiff > /dev/null && alias diff colordiff
alias difft   diff --strip-trailing-cr -ibBw
alias tgz     "tar -czvf \!:1 \!:2"
alias untgz   "tar -xzvf \!:1"
alias tbz2    "tar -cjvf \!:1 \!:2"
alias untbz   "tar -xjvf \!:1"
alias bin2dec 'echo "ibase=2; \!:1" |bc'
alias bin2hex 'echo "obase=16;ibase=2; \!:1" |bc'
alias dec2bin 'echo "obase=2; \!:1" |bc'
alias dec2hex 'echo "obase=16; \!:1" |bc'
alias hex2bin 'echo "ibase=16;obase=2;" `echo \!:1 |tr "[:lower:]" "[:upper:]"` |bc'
alias hex2dec 'echo "ibase=16;" `echo \!:1 |tr "[:lower:]" "[:upper:]"` |bc'

where vim > /dev/null && alias vi  vim

## sshhosts
set sshhosts = (`awk '/^host /{gsub(/host /,"");print}' ~/.ssh/config`)

# local aliases specific to this host
if ( -f ~/.aliases_local) then
  source ~/.aliases_local
endif 

